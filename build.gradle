apply plugin: 'java-library'
apply plugin: 'kotlin'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

def versionTag = "1.0"

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}

repositories {
    jcenter()
}

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "bloder"
            name "Bloder"
            email "aptgetinstalldanielleme@gmail.com"
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            if (plugins.hasPlugin('war')) {
                from components.web
            } else {
                from components.java
            }
            groupId "com.reduks"
            artifactId "reduks"
            version versionTag
            artifact sourcesJar
            artifact javadocJar
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'A simple redux implementation for kotlin apps')
                root.appendNode('name', 'reduks')
                root.appendNode('url', 'https://github.com/Reduks/Reduks')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['mavenJava']
    pkg {
        repo = 'maven'
        name = 'reduks'
        publish = true
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/Reduks/Reduks.git'
        publicDownloadNumbers = true
        version {
            name = versionTag
            desc = 'A simple redux implementation for kotlin apps'
            vcsTag = versionTag
        }
    }
}

dependencies {
    api 'org.apache.commons:commons-math3:3.6.1'
    implementation 'com.google.guava:guava:21.0'
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
}

buildscript {
    ext.kotlin_version = '1.1.4-2'
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.4'
    }
}

